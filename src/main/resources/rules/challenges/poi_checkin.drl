package eu.trentorise.game.model


//list any import classes here.
import eu.trentorise.game.core.Utility;

//declare any global variables here
global Utility utils;

rule "poiReached_rule"
lock-on-active
when
	Action( $id : id == "poi_reached" )
	InputData( $poi : data["poiName"] )
	$challenge : ChallengeConcept( modelName == "poiCheckin", isCompleted() != true, $poiState : fields["poiState"],
		$target : fields["poiName"] )
then
	$challenge.getFields().put("poiState",(Boolean) true);
	update($challenge);
	utils.log("poiReached "+$target);
end


rule "poiCheckin_rule"
lock-on-active
when
	Action( $id : id == 'checkin' ) 
	InputData( $ci : data['checkinName'] )
	$challenge : ChallengeConcept( modelName == "poiCheckin", isCompleted() != true, $eventState : fields["eventState"],
		$target : fields["eventName"] )	
then
	$challenge.getFields().put("eventState", (Boolean) true);
	update($challenge);
	utils.log("poiCheckin "+$target);		
end


rule "ch_poiCheckin_check"
lock-on-active
when
	$challenge : ChallengeConcept( modelName == "poiCheckin", isCompleted() != true, 
		$poiState : fields["poiState"], $eventState : fields["eventState"],
		$bonusPointType : fields["bonusPointType"], $bonusScore : fields["bonusScore"] )
	$pc : PointConcept(name == (String) $bonusPointType)
then
	if ((Boolean)$poiState && (Boolean)$eventState) {
		utils.log('poiCheckin COMPLETED');
		$challenge.completed();
		$pc.setScore($pc.getScore() + (Double) $bonusScore);
		update($pc);
		update($challenge);
	}
end
	
